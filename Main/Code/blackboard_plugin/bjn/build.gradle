apply plugin: "java"
apply plugin: "war"
apply plugin: "maven"
apply plugin: "eclipse"
apply plugin: "idea"

// Uncomment if your project has an API JAR
//apply from: "api.gradle"

// define the version for the project when publishing to maven
group "threepointturn"
version "1.0"

ext {
  learnVersion = "9.1.120113.0"
  springVersion = "4.1.6.RELEASE"
  hibernateVersion = "4.1.10.Final"
  deployServer = "bb.thinkpragmatic.com:9876"
  deployInstance = "BBLEARN"
}

repositories {
  mavenCentral()
  maven {
    url "https://maven.blackboard.com/content/repositories/releases/"
  }
}

configurations {
  buildUtils
}

// define the project's dependencies
dependencies {
  providedCompile "javax.servlet:servlet-api:2.5",
                  "javax.servlet.jsp:jsp-api:2.1"

  // Dependencies are libraries needed to build, but should not be included in the B2 WAR.
  // You should NEVER include Learn JARs (other than webapis) in your B2.
  providedCompile( "blackboard.platform:bb-platform:$ext.learnVersion" ) { transitive = false }
  providedCompile( "blackboard.platform:bb-taglibs:$ext.learnVersion" ) { transitive = false }

  compile( "blackboard.platform:bb-spring-webapi:$ext.learnVersion" ) { transitive = false }

  compile "org.springframework:spring-aop:$ext.springVersion",
          "org.springframework:spring-beans:$ext.springVersion",
          "org.springframework:spring-context:$ext.springVersion",
          "org.springframework:spring-core:$ext.springVersion",
          "org.springframework:spring-expression:$ext.springVersion",
          "org.springframework:spring-web:$ext.springVersion",
          "org.springframework:spring-webmvc:$ext.springVersion",
          "org.springframework:spring-context-support:$ext.springVersion",
          "org.springframework:spring-jdbc:$ext.springVersion",
          "org.springframework:spring-orm:$ext.springVersion",
          "org.springframework:spring-tx:$ext.springVersion"

  compile( "org.hibernate:hibernate-core:$ext.hibernateVersion" ) { transitive = true }
  compile "org.hibernate:hibernate-entitymanager:$ext.hibernateVersion"
  
  compile "org.apache.commons:commons-lang3:3.4"
  
  compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.3'
  compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.3'

  compile "com.fasterxml.jackson.core:jackson-databind:2.5.3"

  // Include the Blackboard security APIs for protecting your B2.
  // providedCompile "blackboard.plugin:security:1.0:api"

  // Dependencies that are only necessary at runtime so should be included in the WAR
  // runtime "org.javassist:javassist:3.17.1-GA"

  buildUtils "org.oscelot:b2deploy-task:0.1.0"
}

// Add a task to deploy a B2 using starting block
task deployB2( dependsOn: "war" ) << {
      ant.taskdef( name: "b2deploy", classname: "org.oscelot.ant.B2DeployTask", classpath: project.configurations.buildUtils.asPath )
      ant.b2deploy( localfilepath: project.war.archivePath, host: project.ext.deployServer, courseorgavailable: 'true', clean: 'true', webappName: 'bb-starting-block-' + project.ext.deployInstance )
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.6'
}
